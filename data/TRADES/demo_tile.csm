# demo_tile
# written by John Dannenhoffer

#           ^ v
#           :
#          010--------011
#          /:         /|
#         / :        / |
#        /  :       /  |
#      110--------111  |
#       |  000 - - |- 001 --> u
#       |  '       |  /
#       | '        | /
#       |'         |/
#      100--------101
#      '
#     w

# default parameters (may be overwritten by udfTile)
despmtr   thkPlate:000    0.10
despmtr   thkPlate:001    0.10
despmtr   thkPlate:010    0.10
despmtr   thkPlate:011    0.10
despmtr   thkPlate:100    0.10
despmtr   thkPlate:101    0.10
despmtr   thkPlate:110    0.10
despmtr   thkPlate:111    0.10

despmtr   thkHot:000      0.04
despmtr   thkHot:001      0.04
despmtr   thkHot:010      0.04
despmtr   thkHot:011      0.04
despmtr   thkHot:100      0.04
despmtr   thkHot:101      0.04
despmtr   thkHot:110      0.04
despmtr   thkHot:111      0.04

despmtr   thkCold:000     0.04
despmtr   thkCold:001     0.04
despmtr   thkCold:010     0.04
despmtr   thkCold:011     0.04
despmtr   thkCold:100     0.04
despmtr   thkCold:101     0.04
despmtr   thkCold:110     0.04
despmtr   thkCold:111     0.04

despmtr   hot2cold:000    0.30
despmtr   hot2cold:001    0.30
despmtr   hot2cold:010    0.30
despmtr   hot2cold:011    0.30
despmtr   hot2cold:100    0.30
despmtr   hot2cold:101    0.30
despmtr   hot2cold:110    0.30
despmtr   hot2cold:111    0.30

# uncomment these to see current values
# set foo   thkPlate:000
# set foo   thkPlate:001
# set foo   thkPlate:010
# set foo   thkPlate:011
# set foo   thkPlate:100
# set foo   thkPlate:101
# set foo   thkPlate:110
# set foo   thkPlate:111

# set foo   thkHot:000
# set foo   thkHot:001
# set foo   thkHot:010
# set foo   thkHot:011
# set foo   thkHot:100
# set foo   thkHot:101
# set foo   thkHot:110
# set foo   thkHot:111

# set foo   thkCold:000
# set foo   thkCold:001
# set foo   thkCold:010
# set foo   thkCold:011
# set foo   thkCold:100
# set foo   thkCold:101
# set foo   thkCold:110
# set foo   thkCold:111

# set foo   hot2cold:000
# set foo   hot2cold:001
# set foo   hot2cold:010
# set foo   hot2cold:011
# set foo   hot2cold:100
# set foo   hot2cold:101
# set foo   hot2cold:110
# set foo   hot2cold:111

# thickness of hot fins
set       thkx00    (thkHot:000+thkHot:100)/2
set       thkx01    (thkHot:001+thkHot:101)/2
set       thkx10    (thkHot:010+thkHot:110)/2
set       thkx11    (thkHot:011+thkHot:111)/2

# thickness of cold fins
set       thk00x    (thkCold:000+thkCold:001)/2 
set       thk01x    (thkCold:010+thkCold:011)/2
set       thk10x    (thkCold:100+thkCold:101)/2
set       thk11x    (thkCold:110+thkCold:111)/2

# thickness of upper and lower plate
set       thk0x0    (thkPlate:000+thkPlate:010)/2
set       thk0x1    (thkPlate:001+thkPlate:011)/2
set       thk1x0    (thkPlate:100+thkPlate:110)/2
set       thk1x1    (thkPlate:101+thkPlate:111)/2

# location of upper and lower plates
set       h2c       (hot2cold:000+hot2cold:010)/2
set       ylo0x0    ((1-2*thk0x0)/(1+h2c)*h2c+thk0x0)/2
set       yup0x0    1-ylo0x0

set       h2c       (hot2cold:001+hot2cold:011)/2
set       ylo0x1    ((1-2*thk0x1)/(1+h2c)*h2c+thk0x1)/2
set       yup0x1    1-ylo0x1

set       h2c       (hot2cold:100+hot2cold:110)/2
set       ylo1x0    ((1-2*thk1x0)/(1+h2c)*h2c+thk1x0)/2
set       yup1x0    1-ylo1x0

set       h2c       (hot2cold:101+hot2cold:111)/2
set       ylo1x1    ((1-2*thk1x1)/(1+h2c)*h2c+thk1x1)/2
set       yup1x1    1-ylo1x1

# upper hot fin (centered at z=0.50)
udprim    hex   corners  "0.0; (yup0x0+yup1x0)/2; 0.5-thkx00/2; \
                          1.0; (yup0x1+yup1x1)/2; 0.5-thkx01/2; \
                          0.0; 1.0;               0.5-thkx10/2; \
                          1.0; 1.0;               0.5-thkx11/2; \
                          0.0; (yup0x0+yup1x0)/2; 0.5+thkx00/2; \
                          1.0; (yup0x1+yup1x1)/2; 0.5+thkx01/2; \
                          0.0; 1.0;               0.5+thkx10/2; \
                          1.0; 1.0;               0.5+thkx11/2"
   select    face   @nbody 5
   attribute _color $red
   select    face   @nbody 6
   attribute _color $red
 
# upper plate (centered at y=yup)
udprim    hex   corners  "0.0; yup0x0-thk0x0/2; 0.0; \
                          1.0; yup0x1-thk0x1/2; 0.0; \
                          0.0; yup0x0+thk0x0/2; 0.0; \
                          1.0; yup0x1+thk0x1/2; 0.0; \
                          0.0; yup1x0-thk1x0/2; 1.0; \
                          1.0; yup1x1-thk1x1/2; 1.0; \
                          0.0; yup1x0+thk1x0/2; 1.0; \
                          1.0; yup1x1+thk1x1/2; 1.0"
   select    face   @nbody 4
   attribute _color $red
   select    face   @nbody 2
   attribute _color $blue
union

# middle cold fin (centered at x=0.50)
udprim    hex   corners  "0.50-thk00x/2; (ylo0x0+ylo0x1)/2; 0.0; \
                          0.50+thk00x/2; (ylo0x0+ylo0x1)/2; 0.0; \
                          0.50-thk01x/2; (yup0x0+yup0x1)/2; 0.0; \
                          0.50+thk01x/2; (yup0x0+yup0x1)/2; 0.0; \
                          0.50-thk10x/2; (ylo1x0+ylo1x1)/2; 1.0; \
                          0.50+thk10x/2; (ylo1x0+ylo1x1)/2; 1.0; \
                          0.50-thk11x/2; (yup1x0+yup1x1)/2; 1.0; \
                          0.50+thk11x/2; (yup1x0+yup1x1)/2; 1.0"
   select    face   @nbody 1
   attribute _color $blue
   select    face   @nbody 3
   attribute _color $blue
union

# lower plate (centered at y=ylo)
udprim    hex   corners  "0.0; ylo0x0-thk0x0/2; 0.0; \
                          1.0; ylo0x1-thk0x1/2; 0.0; \
                          0.0; ylo0x0+thk0x0/2; 0.0; \
                          1.0; ylo0x1+thk0x1/2; 0.0; \
                          0.0; ylo1x0-thk1x0/2; 1.0; \
			  1.0; ylo1x1-thk1x1/2; 1.0; \
                          0.0; ylo1x0+thk1x0/2; 1.0; \
			  1.0; ylo1x1+thk1x1/2; 1.0"
   select    face   @nbody 2
   attribute _color $red
   select    face   @nbody 4
   attribute _color $blue
union

# lower hot fin (centered at z=0.50)
udprim    hex   corners  "0.0; 0.0;               0.5-thkx00/2; \
                          1.0; 0.0;               0.5-thkx01/2; \
                          0.0; (ylo0x0+ylo1x0)/2; 0.5-thkx00/2; \
                          1.0; (ylo0x1+ylo1x1)/2; 0.5-thkx01/2; \
                          0.0; 0.0;               0.5+thkx00/2; \
                          1.0; 0.0;               0.5+thkx01/2; \
                          0.0; (ylo0x0+ylo1x0)/2; 0.5+thkx00/2; \
                          1.0; (ylo0x1+ylo1x1)/2; 0.5+thkx01/2"
   select    face   @nbody 5
   attribute _color $red
   select    face   @nbody 6
   attribute _color $red
union

# make sure there are no poke-throughs
select    body 9
assert         3      @itype
assert        56      @nnode
assert        92      @nedge
assert        38      @nface

end
