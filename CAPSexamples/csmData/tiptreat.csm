# tiptreat
# written by Marshall Galbraith

despmtr   thickness 0.16  # airfoil thickness
despmtr   camber    0.12  # airfoil camber
despmtr   maxloc    0.4   # airfoil max camber location
cfgpmtr   sharpte   1     # aifoil sharp trailing edge

despmtr   span      1.0   # wing semi span
despmtr   taper     0.5   # taper ratip between root and tip
despmtr   sweep     20.   # sweep between quarter span and tip
cfgpmtr   cont      2     # spline continuity at quarter span C0, C1, or C2

despmtr   begFac    10.00 # rounded tip radii
despmtr   endFac    1.00

attribute capsAIM $fun3dAIM;su2AIM;egadsTessAIM;aflr4AIM;pointwiseAIM;tetgenAIM;aflr3AIM #CFD Analysis

set cmean (1+taper)/2
set aspect 2*span/cmean
set area   2*span*cmean

# Set reference values
attribute capsReferenceArea     area
attribute capsReferenceChord    cmean
attribute capsReferenceSpan     2*span
attribute capsMeshLength        cmean #Charachteristic length for meshing

mark
    # rite tip
    udprim naca thickness thickness camber camber maxloc maxloc sharpte sharpte
    scale taper
    translate span*atan(pi(sweep)/180) 0 -span
    
    # C0 rite section
    udprim naca thickness thickness camber camber maxloc maxloc sharpte sharpte
    scale (1+taper)/2
    translate 0 0 -span/2
    store   rite 0 1
    patbeg ndup 2-cont
      restore rite
    patend
    
    # mid section
    udprim naca thickness thickness camber camber maxloc maxloc sharpte sharpte
    
    # C0 left section
    udprim naca thickness thickness camber camber maxloc maxloc sharpte sharpte
    scale (1+taper)/2
    translate 0 0 span/2
    store   left 0 1
    patbeg ndup 2-cont
      restore left
    patend
    
    # left tip
    udprim naca thickness thickness camber camber maxloc maxloc sharpte sharpte
    scale taper
    translate span*atan(pi(sweep)/180) 0 span
blend  -1;begFac  -1;endFac
    attribute capsGroup   $Wing
    attribute _name       $Wing
    attribute AFLR4_Cmp_ID 2

# Create a bounding box as a farfied 
box    -20   -20   -20   40   40  40
    attribute capsGroup   $Farfield
    attribute _name       $Farfield
    attribute AFLR_GBC    $FARFIELD_UG3_GBC
    attribute AFLR4_Cmp_ID 2

end
